buildscript {
    repositories {
        mavenCentral()
    }
    ext {
        compose_ui_version = '1.4.2'
    }
}
plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    // The following line is not strictly necessary but helps with JitPack compatibility
    id 'kotlin-android'
}

android {
    compileSdk 35
    defaultConfig {
        minSdk 26
        targetSdk 35

        aarMetadata {
            minCompileSdk = 31
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        vectorDrawables {
            useSupportLibrary true
        }
    }


    publishing {
        singleVariant('release')
    }

    buildTypes {

        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility "17"
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'com.appversation.appstentcompose'
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

// Custom task to modify the generated POM file
task modifyPom {
    doLast {
        def pomFile = file("${buildDir}/publications/release/pom-default.xml")
        if (pomFile.exists() && pomFile.length() > 0) {
            try {
                def xml = new XmlParser().parse(pomFile)
                if (xml.groupId.size() > 0) {
                    xml.groupId[0].value = 'com.appversation'
                }
                if (xml.artifactId.size() > 0) {
                    xml.artifactId[0].value = 'appstent-compose'
                }
                if (xml.version.size() > 0) {
                    xml.version[0].value = '1.7.5'
                }
                
                pomFile.withWriter { w ->
                    new XmlNodePrinter(new PrintWriter(w)).print(xml)
                }
                println "Successfully modified POM file with correct values"
            } catch (Exception e) {
                println "Error modifying POM file: ${e.message}"
            }
        } else {
            println "POM file does not exist or is empty: ${pomFile.absolutePath}"
        }
    }
}

// Make the modifyPom task depend on the generatePomFileForReleasePublication task
tasks.whenTaskAdded { task ->
    if (task.name == 'generatePomFileForReleasePublication') {
        modifyPom.dependsOn task
    }
    if (task.name == 'publishReleasePublicationToMavenRepository') {
        task.dependsOn modifyPom
    }
}

afterEvaluate {
    publishing {
        repositories {
            // Only add GitHub Packages repository if not building through JitPack
            if (System.getenv("JITPACK") != "true") {
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/Appversation/AppstentCompose")
                    credentials {
                        username = System.getenv("GITHUB_ACTOR") ?: ""
                        password = System.getenv("GITHUB_TOKEN") ?: ""
                    }
                }
            }
        }
        publications {
            release(MavenPublication) {
                groupId = 'com.appversation'
                artifactId = 'appstent-compose'
                version = '1.7.1'

                from components.release
                
                // Add POM metadata
                pom {
                    name = 'appstent-compose'
                    description = 'Appstent Android Compose SDK'
                    url = 'https://github.com/Appversation/AppstentCompose'
                    licenses {
                        license {
                            name = 'Proprietary'
                            url = 'https://github.com/Appversation/AppstentCompose/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'appversation'
                            name = 'Asad Ather'
                            email = 'asad@appversation.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/Appversation/AppstentCompose.git'
                        developerConnection = 'scm:git:ssh://github.com/Appversation/AppstentCompose.git'
                        url = 'https://github.com/Appversation/AppstentCompose'
                    }
                }
            }
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.1'
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation 'androidx.compose.material:material:1.4.2'
    implementation 'androidx.compose.material3:material3:1.1.0-rc01'
    implementation 'androidx.compose.material3:material3-window-size-class:1.1.0-rc01'
    implementation "io.coil-kt:coil-compose:2.2.2"
    implementation 'com.google.android.exoplayer:exoplayer:2.18.6'
    implementation "com.google.accompanist:accompanist-pager:0.23.1"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
}
java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}